buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url = "http://repo.supaham.com/nexus/content/repositories/releases/" }
        maven { url = "http://repo.supaham.com/nexus/content/repositories/snapshots/" }
        maven { url = "http://repo.onarandombox.com/content/groups/public/" }
        jcenter()
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.0"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

println """
*******************************************
 You are building commons! 

 If you encounter trouble:
 1) Read COMPILING.md if you haven't yet
 2) Try running 'build' in a separate Gradle run
 3) Use gradlew and not gradle

 Output files will be in [module]/build/libs
*******************************************
"""

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'com.supaham.commons'
    version = '0.4-SNAPSHOT'
    def nexusRepoUrl
    
    if (!version.endsWith("-SNAPSHOT")) {
        nexusRepoUrl = "http://repo.supaham.com/nexus/content/repositories/releases/"
    } else {
        nexusRepoUrl = "http://repo.supaham.com/nexus/content/repositories/snapshots/"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "http://repo.onarandombox.com/content/groups/public/" }
        // Thanks NathanWolf <3
        maven { url = "http://maven.elmakers.com/repository/" }
    }
    
    ext.internalVersion = version
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: classes) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    build.dependsOn sourcesJar, javadocJar

    shadowJar {
        classifier 'dist'
    }
    
    uploadArchives {
        repositories {
            mavenDeployer {
//                Goodbye ol' friend.
//                repository(url: nexusRepoUrl) {
//                    authentication(userName: supahamNexusUser, password: supahamNexusPass)
//                }
                
                pom.project {
                    packaging 'jar'
                    
                    scm {
                        url 'git@github.com:SupaHam/SupaCommons.git'
                        connection 'scm:git:git@github.com:SupaHam/SupaCommons.git'
                        developerConnection 'scm:git:git@github.com:SupaHam/SupaCommons.git'
                    }

                    licenses {
                        license {
                            name 'GNU GENERAL PUBLIC LICENSE Version 3'
                            url 'https://www.gnu.org/licenses/gpl.html'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'SupaHam'
                            name 'Ali Moghnieh'
                            email 'me@supa.me'
                            url 'http://supaham.com'
                            timezone 'Europe/London'
                        }
                    }
                }
            }
        }
    }
}
